public class io/github/lukebemish/lesslazyitems/LessLazyItems {

    public final static MODID java/lang/String = "lesslazyitems"

    public final static LOGGER org/slf4j/Logger

    public final static ITEMS net/minecraftforge/registries/DeferredRegister
    public final static ENTITIES net/minecraftforge/registries/DeferredRegister

    public final static ITEM_WAND net/minecraftforge/registries/RegistryObject
    public final static PERSISTENT_ITEM_WAND net/minecraftforge/registries/RegistryObject
    public final static WALKING_ITEM net/minecraftforge/registries/RegistryObject

    public final static COLLECTORS net/minecraft/tags/TagKey

    public final static SHULKER_ITEMS java/util/Set
    public final static SHULKER_CAP_RL net/minecraft/resources/ResourceLocation

    public <init>()V {
        aload 0
        invokespecial java/lang/Object.<init>()V

        invokestatic net/minecraftforge/fml/javafmlmod/FMLJavaModLoadingContext.get()net/minecraftforge/fml/javafmlmod/FMLJavaModLoadingContext
        invokevirtual net/minecraftforge/fml/javafmlmod/FMLJavaModLoadingContext.getModEventBus()net/minecraftforge/eventbus/api/IEventBus
        astore 1

        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ITEMS net/minecraftforge/registries/DeferredRegister
        aload 1
        invokevirtual net/minecraftforge/registries/DeferredRegister.register(net/minecraftforge/eventbus/api/IEventBus)V

        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ENTITIES net/minecraftforge/registries/DeferredRegister
        aload 1
        invokevirtual net/minecraftforge/registries/DeferredRegister.register(net/minecraftforge/eventbus/api/IEventBus)V

        invokestatic net/minecraftforge/fml/loading/FMLLoader.getDist()net/minecraftforge/api/distmarker/Dist
        getstatic net/minecraftforge/api/distmarker/Dist.CLIENT net/minecraftforge/api/distmarker/Dist

        if_acmpne skip1
        aload 1
        invokedynamic accept()java/util/function/Consumer {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                (java/lang/Object)V,
                invokestatic io/github/lukebemish/lesslazyitems/LessLazyItemsClient.registerRenderers(net/minecraftforge/client/event/EntityRenderersEvent$RegisterRenderers)V,
                (net/minecraftforge/client/event/EntityRenderersEvent$RegisterRenderers)V
            ]
        }
        invokeinterface net/minecraftforge/eventbus/api/IEventBus.addListener(java/util/function/Consumer)V

        skip1:

        aload 1
        invokedynamic accept()java/util/function/Consumer {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                (java/lang/Object)V,
                invokestatic io/github/lukebemish/lesslazyitems/LessLazyItems.lambda2(net/minecraftforge/event/entity/EntityAttributeCreationEvent)V,
                (net/minecraftforge/event/entity/EntityAttributeCreationEvent)V
            ]
        }
        invokeinterface net/minecraftforge/eventbus/api/IEventBus.addListener(java/util/function/Consumer)V

        aload 1
        invokedynamic accept()java/util/function/Consumer {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                (java/lang/Object)V,
                invokestatic io/github/lukebemish/lesslazyitems/LessLazyItems.lambda3(net/minecraftforge/fml/event/lifecycle/FMLCommonSetupEvent)V,
                (net/minecraftforge/fml/event/lifecycle/FMLCommonSetupEvent)V
            ]
        }
        invokeinterface net/minecraftforge/eventbus/api/IEventBus.addListener(java/util/function/Consumer)V

        getstatic net/minecraftforge/common/MinecraftForge.EVENT_BUS net/minecraftforge/eventbus/api/IEventBus
        ldc net/minecraft/world/item/ItemStack
        invokedynamic accept()java/util/function/Consumer {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                (java/lang/Object)V,
                invokestatic io/github/lukebemish/lesslazyitems/LessLazyItems.lambda5(net/minecraftforge/event/AttachCapabilitiesEvent)V,
                (net/minecraftforge/event/AttachCapabilitiesEvent)V
            ]
        }
        invokeinterface net/minecraftforge/eventbus/api/IEventBus.addGenericListener(java/lang/Class,java/util/function/Consumer)V

        return
    }

    static <clinit>()V {
        invokestatic com/mojang/logging/LogUtils.getLogger()org/slf4j/Logger
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.LOGGER org/slf4j/Logger

        getstatic net/minecraftforge/registries/ForgeRegistries.ENTITY_TYPES net/minecraftforge/registries/IForgeRegistry
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.MODID java/lang/String
        invokestatic net/minecraftforge/registries/DeferredRegister.create(net/minecraftforge/registries/IForgeRegistry,java/lang/String)net/minecraftforge/registries/DeferredRegister
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ENTITIES net/minecraftforge/registries/DeferredRegister

        getstatic net/minecraftforge/registries/ForgeRegistries.ITEMS net/minecraftforge/registries/IForgeRegistry
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.MODID java/lang/String
        invokestatic net/minecraftforge/registries/DeferredRegister.create(net/minecraftforge/registries/IForgeRegistry,java/lang/String)net/minecraftforge/registries/DeferredRegister
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ITEMS net/minecraftforge/registries/DeferredRegister


        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ITEMS net/minecraftforge/registries/DeferredRegister
        ldc "item_wand"
        invokedynamic get()java/util/function/Supplier {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                ()java/lang/Object,
                invokestatic io/github/lukebemish/lesslazyitems/LessLazyItems.lambda0()net/minecraft/world/item/Item,
                ()net/minecraft/world/item/Item
            ]
        }
        invokevirtual net/minecraftforge/registries/DeferredRegister.register(java/lang/String,java/util/function/Supplier)net/minecraftforge/registries/RegistryObject
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ITEM_WAND net/minecraftforge/registries/RegistryObject

        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ITEMS net/minecraftforge/registries/DeferredRegister
        ldc "persistent_item_wand"
        invokedynamic get()java/util/function/Supplier {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                ()java/lang/Object,
                invokestatic io/github/lukebemish/lesslazyitems/LessLazyItems.lambda4()net/minecraft/world/item/Item,
                ()net/minecraft/world/item/Item
            ]
        }
        invokevirtual net/minecraftforge/registries/DeferredRegister.register(java/lang/String,java/util/function/Supplier)net/minecraftforge/registries/RegistryObject
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.PERSISTENT_ITEM_WAND net/minecraftforge/registries/RegistryObject

        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ENTITIES net/minecraftforge/registries/DeferredRegister
        ldc "walking_item"
        invokedynamic get()java/util/function/Supplier {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                ()java/lang/Object,
                invokestatic io/github/lukebemish/lesslazyitems/LessLazyItems.lambda1()net/minecraft/world/entity/EntityType,
                ()net/minecraft/world/entity/EntityType
            ]
        }
        invokevirtual net/minecraftforge/registries/DeferredRegister.register(java/lang/String,java/util/function/Supplier)net/minecraftforge/registries/RegistryObject
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.WALKING_ITEM net/minecraftforge/registries/RegistryObject


        new net/minecraft/resources/ResourceLocation
        dup
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.MODID java/lang/String
        ldc "collectors"
        invokespecial net/minecraft/resources/ResourceLocation.<init>(java/lang/String, java/lang/String)V
        invokestatic net/minecraft/tags/BlockTags.create(net/minecraft/resources/ResourceLocation)net/minecraft/tags/TagKey
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.COLLECTORS net/minecraft/tags/TagKey

        // Shulker set
        bipush 17
        anewarray net/minecraft/world/item/Item
        dup
        getstatic net/minecraft/world/item/Items.SHULKER_BOX net/minecraft/world/item/Item
        iconst 0 swap aastore dup
        getstatic net/minecraft/world/item/Items.BLACK_SHULKER_BOX net/minecraft/world/item/Item
        iconst 1 swap aastore dup
        getstatic net/minecraft/world/item/Items.BLUE_SHULKER_BOX net/minecraft/world/item/Item
        iconst 2 swap aastore dup
        getstatic net/minecraft/world/item/Items.BROWN_SHULKER_BOX net/minecraft/world/item/Item
        bipush 3 swap aastore dup
        getstatic net/minecraft/world/item/Items.CYAN_SHULKER_BOX net/minecraft/world/item/Item
        bipush 4 swap aastore dup
        getstatic net/minecraft/world/item/Items.GRAY_SHULKER_BOX net/minecraft/world/item/Item
        bipush 5 swap aastore dup
        getstatic net/minecraft/world/item/Items.GREEN_SHULKER_BOX net/minecraft/world/item/Item
        bipush 6 swap aastore dup
        getstatic net/minecraft/world/item/Items.LIGHT_BLUE_SHULKER_BOX net/minecraft/world/item/Item
        bipush 7 swap aastore dup
        getstatic net/minecraft/world/item/Items.LIGHT_GRAY_SHULKER_BOX net/minecraft/world/item/Item
        bipush 8 swap aastore dup
        getstatic net/minecraft/world/item/Items.LIME_SHULKER_BOX net/minecraft/world/item/Item
        bipush 9 swap aastore dup
        getstatic net/minecraft/world/item/Items.MAGENTA_SHULKER_BOX net/minecraft/world/item/Item
        bipush 10 swap aastore dup
        getstatic net/minecraft/world/item/Items.ORANGE_SHULKER_BOX net/minecraft/world/item/Item
        bipush 11 swap aastore dup
        getstatic net/minecraft/world/item/Items.PINK_SHULKER_BOX net/minecraft/world/item/Item
        bipush 12 swap aastore dup
        getstatic net/minecraft/world/item/Items.PURPLE_SHULKER_BOX net/minecraft/world/item/Item
        bipush 13 swap aastore dup
        getstatic net/minecraft/world/item/Items.RED_SHULKER_BOX net/minecraft/world/item/Item
        bipush 14 swap aastore dup
        getstatic net/minecraft/world/item/Items.WHITE_SHULKER_BOX net/minecraft/world/item/Item
        bipush 15 swap aastore dup
        getstatic net/minecraft/world/item/Items.YELLOW_SHULKER_BOX net/minecraft/world/item/Item
        bipush 16 swap aastore
        invokestatic* java/util/Set.of([java/lang/Object)java/util/Set
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.SHULKER_ITEMS java/util/Set

        new net/minecraft/resources/ResourceLocation
        dup
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.MODID java/lang/String
        ldc "shulker_capability"
        invokespecial net/minecraft/resources/ResourceLocation.<init>(java/lang/String,java/lang/String)V
        putstatic io/github/lukebemish/lesslazyitems/LessLazyItems.SHULKER_CAP_RL net/minecraft/resources/ResourceLocation

        return
    }

    public static synthetic lambda0()net/minecraft/world/item/Item {
        new io/github/lukebemish/lesslazyitems/ItemWandItem
        dup
        new net/minecraft/world/item/Item$Properties
        dup
        invokespecial net/minecraft/world/item/Item$Properties.<init>()V
        getstatic net/minecraft/world/item/CreativeModeTab.TAB_TOOLS net/minecraft/world/item/CreativeModeTab
        invokevirtual net/minecraft/world/item/Item$Properties.tab(net/minecraft/world/item/CreativeModeTab)net/minecraft/world/item/Item$Properties
        iconst 1
        invokevirtual net/minecraft/world/item/Item$Properties.stacksTo(I)net/minecraft/world/item/Item$Properties
        iconst 0
        invokespecial io/github/lukebemish/lesslazyitems/ItemWandItem.<init>(net/minecraft/world/item/Item$Properties,Z)V
        areturn
    }

    public static synthetic lambda4()net/minecraft/world/item/Item {
        new io/github/lukebemish/lesslazyitems/ItemWandItem
        dup
        new net/minecraft/world/item/Item$Properties
        dup
        invokespecial net/minecraft/world/item/Item$Properties.<init>()V
        getstatic net/minecraft/world/item/CreativeModeTab.TAB_TOOLS net/minecraft/world/item/CreativeModeTab
        invokevirtual net/minecraft/world/item/Item$Properties.tab(net/minecraft/world/item/CreativeModeTab)net/minecraft/world/item/Item$Properties
        iconst 1
        invokevirtual net/minecraft/world/item/Item$Properties.stacksTo(I)net/minecraft/world/item/Item$Properties
        iconst 1
        invokespecial io/github/lukebemish/lesslazyitems/ItemWandItem.<init>(net/minecraft/world/item/Item$Properties,Z)V
        areturn
    }

    public static synthetic lambda1()net/minecraft/world/entity/EntityType {
        invokedynamic create()net/minecraft/world/entity/EntityType$EntityFactory {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                (net/minecraft/world/entity/EntityType,net/minecraft/world/level/Level)net/minecraft/world/entity/Entity,
                newinvokespecial io/github/lukebemish/lesslazyitems/WalkingItemEntity.<init>(net/minecraft/world/entity/EntityType,net/minecraft/world/level/Level)V,
                (net/minecraft/world/entity/EntityType,net/minecraft/world/level/Level)io/github/lukebemish/lesslazyitems/WalkingItemEntity
            ]
        }
        getstatic net/minecraft/world/entity/MobCategory.MISC net/minecraft/world/entity/MobCategory
        invokestatic net/minecraft/world/entity/EntityType$Builder.of(net/minecraft/world/entity/EntityType$EntityFactory, net/minecraft/world/entity/MobCategory)net/minecraft/world/entity/EntityType$Builder
        ldc .25
        ldc .25
        invokevirtual net/minecraft/world/entity/EntityType$Builder.sized(F,F)net/minecraft/world/entity/EntityType$Builder
        bipush 8
        invokevirtual net/minecraft/world/entity/EntityType$Builder.clientTrackingRange(I)net/minecraft/world/entity/EntityType$Builder
        ldc "lesslazyitems:walking_item"
        invokevirtual net/minecraft/world/entity/EntityType$Builder.build(java/lang/String)net/minecraft/world/entity/EntityType
        areturn
    }

    public static synthetic lambda2(net/minecraftforge/event/entity/EntityAttributeCreationEvent)V {
        aload 0
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.WALKING_ITEM net/minecraftforge/registries/RegistryObject
        invokeinterface java/util/function/Supplier.get()java/lang/Object
        checkcast net/minecraft/world/entity/EntityType
        invokestatic io/github/lukebemish/lesslazyitems/WalkingItemEntity.createAttributes()net/minecraft/world/entity/ai/attributes/AttributeSupplier$Builder
        invokevirtual net/minecraft/world/entity/ai/attributes/AttributeSupplier$Builder.build()net/minecraft/world/entity/ai/attributes/AttributeSupplier
        invokevirtual net/minecraftforge/event/entity/EntityAttributeCreationEvent.put(net/minecraft/world/entity/EntityType, net/minecraft/world/entity/ai/attributes/AttributeSupplier)V

        return
    }

    public static synthetic lambda3(net/minecraftforge/fml/event/lifecycle/FMLCommonSetupEvent)V {
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.ITEM_WAND net/minecraftforge/registries/RegistryObject
        invokevirtual net/minecraftforge/registries/RegistryObject.get()java/lang/Object
        checkcast net/minecraft/world/item/Item

        new io/github/lukebemish/lesslazyitems/ItemWandDispenserBehavior
        dup
        iconst 0
        invokespecial io/github/lukebemish/lesslazyitems/ItemWandDispenserBehavior.<init>(Z)V

        invokestatic net/minecraft/world/level/block/DispenserBlock.registerBehavior(net/minecraft/world/level/ItemLike,net/minecraft/core/dispenser/DispenseItemBehavior)V



        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.PERSISTENT_ITEM_WAND net/minecraftforge/registries/RegistryObject
        invokevirtual net/minecraftforge/registries/RegistryObject.get()java/lang/Object
        checkcast net/minecraft/world/item/Item

        new io/github/lukebemish/lesslazyitems/ItemWandDispenserBehavior
        dup
        iconst 1
        invokespecial io/github/lukebemish/lesslazyitems/ItemWandDispenserBehavior.<init>(Z)V

        invokestatic net/minecraft/world/level/block/DispenserBlock.registerBehavior(net/minecraft/world/level/ItemLike,net/minecraft/core/dispenser/DispenseItemBehavior)V


        return
    }

    public static synthetic lambda5(net/minecraftforge/event/AttachCapabilitiesEvent)V {
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.SHULKER_ITEMS java/util/Set
        aload 0
        invokevirtual net/minecraftforge/event/AttachCapabilitiesEvent.getObject()java/lang/Object
        checkcast net/minecraft/world/item/ItemStack
        dup astore 1
        invokevirtual net/minecraft/world/item/ItemStack.getItem()net/minecraft/world/item/Item
        invokeinterface java/util/Set.contains(java/lang/Object)Z
        ifeq out
        aload 0
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.SHULKER_CAP_RL net/minecraft/resources/ResourceLocation
        new io/github/lukebemish/lesslazyitems/ShulkerCapabilityWrapper
        dup
        aload 1
        invokespecial io/github/lukebemish/lesslazyitems/ShulkerCapabilityWrapper.<init>(net/minecraft/world/item/ItemStack)V
        invokevirtual net/minecraftforge/event/AttachCapabilitiesEvent.addCapability(net/minecraft/resources/ResourceLocation,net/minecraftforge/common/capabilities/ICapabilityProvider)V
        out:
        return
    }
}