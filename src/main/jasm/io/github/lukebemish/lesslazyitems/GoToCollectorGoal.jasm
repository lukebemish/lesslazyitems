public class io/github/lukebemish/lesslazyitems/GoToCollectorGoal extends net/minecraft/world/entity/ai/goal/MoveToBlockGoal {
    public <init>(io/github/lukebemish/lesslazyitems/WalkingItemEntity,double)V {
        aload 0
        aload 1
        dload 2
        bipush 16
        bipush 5
        invokespecial net/minecraft/world/entity/ai/goal/MoveToBlockGoal.<init>(net/minecraft/world/entity/PathfinderMob,double,int,I)V

        return
    }

    protected isValidTarget(net/minecraft/world/level/LevelReader,net/minecraft/core/BlockPos)Z {
        aload 1
        aload 2
        invokevirtual net/minecraft/core/BlockPos.above()net/minecraft/core/BlockPos
        invokeinterface net/minecraft/world/level/LevelReader.isEmptyBlock(net/minecraft/core/BlockPos)Z
        ifeq invalid
        aload 1
        aload 2
        invokeinterface net/minecraft/world/level/LevelReader.getBlockState(net/minecraft/core/BlockPos)net/minecraft/world/level/block/state/BlockState
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.COLLECTORS net/minecraft/tags/TagKey
        invokevirtual net/minecraft/world/level/block/state/BlockState.is(net/minecraft/tags/TagKey)Z
        goto out

        invalid:
        iconst 0

        out:
        ireturn
    }

    protected nextStartTick(net/minecraft/world/entity/PathfinderMob)I {
        bipush 20
        ireturn
    }

    public acceptedDistance()D {
        ldc 0.1
        f2d
        dreturn
    }

}