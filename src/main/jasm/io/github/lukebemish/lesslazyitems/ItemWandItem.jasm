public class io/github/lukebemish/lesslazyitems/ItemWandItem extends net/minecraft/world/item/Item {
    public <init>(net/minecraft/world/item/Item$Properties)V {
        aload 0
        aload 1
        invokespecial net/minecraft/world/item/Item.<init>(net/minecraft/world/item/Item$Properties)V
        return
    }

    public static convertNearbyItems(net/minecraft/world/level/Level,net/minecraft/core/BlockPos)V {
        aload 0
        ldc net/minecraft/world/entity/item/ItemEntity
        aload 1
        invokestatic net/minecraft/world/phys/Vec3.atCenterOf(net/minecraft/core/Vec3i)net/minecraft/world/phys/Vec3
        ldc 7
        i2d
        ldc 7
        i2d
        ldc 7
        i2d
        invokestatic net/minecraft/world/phys/AABB.ofSize(net/minecraft/world/phys/Vec3,D,D,D)net/minecraft/world/phys/AABB
        invokedynamic test()java/util/function/Predicate {
            invokestatic java/lang/invoke/LambdaMetafactory.metafactory(
                java/lang/invoke/MethodHandles$Lookup,
                java/lang/String,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodType,
                java/lang/invoke/MethodHandle,
                java/lang/invoke/MethodType,
            )java/lang/invoke/CallSite
            [
                (java/lang/Object)Z,
                invokestatic io/github/lukebemish/lesslazyitems/ItemWandItem.lambda0(java/lang/Object)Z,
                (java/lang/Object)Z
            ]
        }
        invokeinterface net/minecraft/world/level/EntityGetter.getEntitiesOfClass(java/lang/Class,net/minecraft/world/phys/AABB,java/util/function/Predicate)java/util/List

        invokeinterface java/lang/Iterable.iterator()java/util/Iterator
        astore 2
        loop:
        aload 2
        invokeinterface java/util/Iterator.hasNext()Z
        ifeq break
        aload 2
        invokeinterface java/util/Iterator.next()java/lang/Object
        checkcast net/minecraft/world/entity/item/ItemEntity
        astore 3

        aload 3
        invokevirtual net/minecraft/world/entity/item/ItemEntity.position()net/minecraft/world/phys/Vec3
        astore 4

        aload 0
        new io/github/lukebemish/lesslazyitems/WalkingItemEntity
        dup
        dup
        aload 0
        aload 3
        invokespecial io/github/lukebemish/lesslazyitems/WalkingItemEntity.<init>(net/minecraft/world/level/Level,net/minecraft/world/entity/item/ItemEntity)V
        aload 4
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.setPos(net/minecraft/world/phys/Vec3)V

        invokeinterface net/minecraft/world/level/LevelWriter.addFreshEntity(net/minecraft/world/entity/Entity)Z
        pop

        //getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.LOGGER org/slf4j/Logger
        //ldc "Converted"
        //invokeinterface org/slf4j/Logger.info(java/lang/String)V

        goto loop

        break:
        return
    }

    public static synthetic lambda0(java/lang/Object)Z {
        iconst 1
        ireturn
    }

    public useOn(net/minecraft/world/item/context/UseOnContext)net/minecraft/world/InteractionResult {
        aload 1
        invokevirtual net/minecraft/world/item/context/UseOnContext.getLevel()net/minecraft/world/level/Level
        astore 2
        aload 2
        invokevirtual net/minecraft/world/level/Level.isClientSide()Z
        dup
        ifne skip
        aload 2
        aload 1
        invokevirtual net/minecraft/world/item/context/UseOnContext.getClickedPos()net/minecraft/core/BlockPos
        invokestatic io/github/lukebemish/lesslazyitems/ItemWandItem.convertNearbyItems(net/minecraft/world/level/Level,net/minecraft/core/BlockPos)V

        skip:

        invokestatic net/minecraft/world/InteractionResult.sidedSuccess(Z)net/minecraft/world/InteractionResult

        areturn
    }
}