public class io/github/lukebemish/lesslazyitems/WalkingItemEntityModel extends net/minecraft/client/model/EntityModel {
    private final rightLeg net/minecraft/client/model/geom/ModelPart
    private final leftLeg net/minecraft/client/model/geom/ModelPart

    public <init>(net/minecraft/client/model/geom/ModelPart)V {
        aload 0
        invokespecial net/minecraft/client/model/EntityModel.<init>()V

        aload 0
        aload 1
        ldc "right_leg"
        invokevirtual net/minecraft/client/model/geom/ModelPart.getChild(java/lang/String)net/minecraft/client/model/geom/ModelPart
        putfield io/github/lukebemish/lesslazyitems/WalkingItemEntityModel.rightLeg net/minecraft/client/model/geom/ModelPart

        aload 0
        aload 1
        ldc "left_leg"
        invokevirtual net/minecraft/client/model/geom/ModelPart.getChild(java/lang/String)net/minecraft/client/model/geom/ModelPart
        putfield io/github/lukebemish/lesslazyitems/WalkingItemEntityModel.leftLeg net/minecraft/client/model/geom/ModelPart

        return
    }

    public static createBodyLayer()net/minecraft/client/model/geom/builders/LayerDefinition {
        new net/minecraft/client/model/geom/builders/MeshDefinition
        dup
        invokespecial net/minecraft/client/model/geom/builders/MeshDefinition.<init>()V
        dup
        astore 0
        invokevirtual net/minecraft/client/model/geom/builders/MeshDefinition.getRoot()net/minecraft/client/model/geom/builders/PartDefinition
        astore 1
        invokestatic net/minecraft/client/model/geom/builders/CubeListBuilder.create()net/minecraft/client/model/geom/builders/CubeListBuilder
        bipush 26
        iconst 0
        invokevirtual net/minecraft/client/model/geom/builders/CubeListBuilder.texOffs(I,I)net/minecraft/client/model/geom/builders/CubeListBuilder
        ldc -1.0
        fconst 0
        ldc -3.0
        ldc 3.0
        ldc 5.0
        ldc 3.0
        invokevirtual net/minecraft/client/model/geom/builders/CubeListBuilder.addBox(F,F,F,F,F,F)net/minecraft/client/model/geom/builders/CubeListBuilder
        astore 2

        // right
        aload 1
        ldc "right_leg"
        aload 2
        ldc -2.0
        ldc 19.0
        fconst 1
        invokestatic net/minecraft/client/model/geom/PartPose.offset(F,F,F,)net/minecraft/client/model/geom/PartPose
        invokevirtual net/minecraft/client/model/geom/builders/PartDefinition.addOrReplaceChild(java/lang/String,net/minecraft/client/model/geom/builders/CubeListBuilder,net/minecraft/client/model/geom/PartPose)net/minecraft/client/model/geom/builders/PartDefinition
        pop

        // left
        aload 1
        ldc "left_leg"
        aload 2
        fconst 1
        ldc 19.0
        fconst 1
        invokestatic net/minecraft/client/model/geom/PartPose.offset(F,F,F,)net/minecraft/client/model/geom/PartPose
        invokevirtual net/minecraft/client/model/geom/builders/PartDefinition.addOrReplaceChild(java/lang/String,net/minecraft/client/model/geom/builders/CubeListBuilder,net/minecraft/client/model/geom/PartPose)net/minecraft/client/model/geom/builders/PartDefinition
        pop

        aload 0
        bipush 64
        bipush 32
        invokestatic net/minecraft/client/model/geom/builders/LayerDefinition.create(net/minecraft/client/model/geom/builders/MeshDefinition,I,I)net/minecraft/client/model/geom/builders/LayerDefinition
        areturn
        // Basically copied stuff from the chicken here, since I'm literally using the same leg setup.
    }

    public renderToBuffer(com/mojang/blaze3d/vertex/PoseStack,com/mojang/blaze3d/vertex/VertexConsumer,I,I,F,F,F,F)V {
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntityModel.rightLeg net/minecraft/client/model/geom/ModelPart
        aload 1
        aload 2
        iload 3
        iload 4
        fload 5
        fload 6
        fload 7
        fload 8
        invokevirtual net/minecraft/client/model/geom/ModelPart.render(com/mojang/blaze3d/vertex/PoseStack,com/mojang/blaze3d/vertex/VertexConsumer,I,I,F,F,F,F)V

        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntityModel.leftLeg net/minecraft/client/model/geom/ModelPart
        aload 1
        aload 2
        iload 3
        iload 4
        fload 5
        fload 6
        fload 7
        fload 8
        invokevirtual net/minecraft/client/model/geom/ModelPart.render(com/mojang/blaze3d/vertex/PoseStack,com/mojang/blaze3d/vertex/VertexConsumer,I,I,F,F,F,F)V

        return
    }

    public setupAnim(net/minecraft/world/entity/Entity,F,F,F,F,F)V {
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntityModel.rightLeg net/minecraft/client/model/geom/ModelPart
        fload 2
        ldc 0.6662
        fmul
        invokestatic net/minecraft/util/Mth.cos(F)F
        ldc 1.4
        fmul
        fload 3
        fmul
        putfield net/minecraft/client/model/geom/ModelPart.xRot F

        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntityModel.leftLeg net/minecraft/client/model/geom/ModelPart
        fload 2
        ldc 0.6662
        fmul
        ldc 3.1415927
        fadd
        invokestatic net/minecraft/util/Mth.cos(F)F
        ldc 1.4
        fmul
        fload 3
        fmul
        putfield net/minecraft/client/model/geom/ModelPart.xRot F

        return
    }
}