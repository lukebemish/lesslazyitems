public class io/github/lukebemish/lesslazyitems/WalkingItemEntity extends net/minecraft/world/entity/PathfinderMob {
    private static final DATA_ITEM net/minecraft/network/syncher/EntityDataAccessor

    private age int
    private itemAge int
    public final bobOffs float

    public <init>(net/minecraft/world/entity/EntityType,net/minecraft/world/level/Level)V {
        aload 0
        aload 1
        aload 2
        invokespecial net/minecraft/world/entity/PathfinderMob.<init>(net/minecraft/world/entity/EntityType,net/minecraft/world/level/Level)V

        aload 0
        iconst 0
        putfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.xpReward int

        aload 0
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.random net/minecraft/util/RandomSource
        invokeinterface net/minecraft/util/RandomSource.nextFloat()F
        getstatic java/lang/Math.PI D
        d2f
        fmul
        ldc 2.0
        fmul
        putfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.bobOffs F

        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.setPersistenceRequired()V

        return
    }

    public <init>(net/minecraft/world/level/Level,net/minecraft/world/entity/item/ItemEntity)V {
        aload 0
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.WALKING_ITEM net/minecraftforge/registries/RegistryObject
        invokeinterface java/util/function/Supplier.get()java/lang/Object
        checkcast net/minecraft/world/entity/EntityType
        aload 1
        invokespecial io/github/lukebemish/lesslazyitems/WalkingItemEntity.<init>(net/minecraft/world/entity/EntityType,net/minecraft/world/level/Level)V

        aload 0
        aload 2
        invokevirtual net/minecraft/world/entity/item/ItemEntity.getItem()net/minecraft/world/item/ItemStack
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.setItem(net/minecraft/world/item/ItemStack)V

        aload 0
        aload 2
        invokevirtual net/minecraft/world/entity/item/ItemEntity.getAge()I
        putfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.itemAge I

        aload 2
        invokevirtual net/minecraft/world/entity/item/ItemEntity.discard()V

        return
    }

    public getStandingEyeHeight(net/minecraft/world/entity/Pose,net/minecraft/world/entity/EntityDimensions)F {
        aload 2
        getfield net/minecraft/world/entity/EntityDimensions.height F
        ldc 0.5
        fmul
        freturn
    }

    public tick()V {
        aload 0
        invokespecial net/minecraft/world/entity/PathfinderMob.tick()V
        aload 0
        dup
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.age I
        iconst 1
        iadd
        putfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.age I

        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.level net/minecraft/world/level/Level
        invokevirtual net/minecraft/world/level/Level.isClientSide()Z
        ifne skip1

        new net/minecraft/core/BlockPos
        dup
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.position()net/minecraft/world/phys/Vec3
        invokespecial net/minecraft/core/BlockPos.<init>(net/minecraft/world/phys/Vec3)V
        astore 1

        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.level net/minecraft/world/level/Level
        aload 1
        invokevirtual net/minecraft/core/BlockPos.above()net/minecraft/core/BlockPos
        invokeinterface net/minecraft/world/level/LevelReader.getBlockState(net/minecraft/core/BlockPos)net/minecraft/world/level/block/state/BlockState
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.COLLECTORS net/minecraft/tags/TagKey
        invokevirtual net/minecraft/world/level/block/state/BlockState.is(net/minecraft/tags/TagKey)Z
        ifne run1

        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.level net/minecraft/world/level/Level
        aload 1
        invokeinterface net/minecraft/world/level/LevelReader.getBlockState(net/minecraft/core/BlockPos)net/minecraft/world/level/block/state/BlockState
        getstatic io/github/lukebemish/lesslazyitems/LessLazyItems.COLLECTORS net/minecraft/tags/TagKey
        invokevirtual net/minecraft/world/level/block/state/BlockState.is(net/minecraft/tags/TagKey)Z
        ifne run1

        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.age I
        ldc 1200
        if_icmpge run1
        goto skip1

        run1:
        aload 0
        dup
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.createItemEntity()V
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.discard()V

        skip1:

        return
    }

    public static <clinit>()V {
        ldc io/github/lukebemish/lesslazyitems/WalkingItemEntity
        getstatic net/minecraft/network/syncher/EntityDataSerializers.ITEM_STACK net/minecraft/network/syncher/EntityDataSerializer
        invokestatic net/minecraft/network/syncher/SynchedEntityData.defineId(java/lang/Class,net/minecraft/network/syncher/EntityDataSerializer)net/minecraft/network/syncher/EntityDataAccessor
        putstatic io/github/lukebemish/lesslazyitems/WalkingItemEntity.DATA_ITEM net/minecraft/network/syncher/EntityDataAccessor

        return
    }

    protected registerGoals()V {
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.goalSelector net/minecraft/world/entity/ai/goal/GoalSelector
        iconst 1
        new io/github/lukebemish/lesslazyitems/GoToCollectorGoal
        dup
        aload 0
        dconst 1
        invokespecial io/github/lukebemish/lesslazyitems/GoToCollectorGoal.<init>(io/github/lukebemish/lesslazyitems/WalkingItemEntity,double)V
        invokevirtual net/minecraft/world/entity/ai/goal/GoalSelector.addGoal(int,net/minecraft/world/entity/ai/goal/Goal)V

        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.goalSelector net/minecraft/world/entity/ai/goal/GoalSelector
        iconst 2
        new net/minecraft/world/entity/ai/goal/RandomStrollGoal
        dup
        aload 0
        ldc 0.75
        f2d
        invokespecial net/minecraft/world/entity/ai/goal/RandomStrollGoal.<init>(net/minecraft/world/entity/PathfinderMob,D)V
        invokevirtual net/minecraft/world/entity/ai/goal/GoalSelector.addGoal(int,net/minecraft/world/entity/ai/goal/Goal)V

        return
    }

    protected defineSynchedData()V {
        aload 0
        invokespecial net/minecraft/world/entity/PathfinderMob.defineSynchedData()V
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getEntityData()net/minecraft/network/syncher/SynchedEntityData
        getstatic io/github/lukebemish/lesslazyitems/WalkingItemEntity.DATA_ITEM net/minecraft/network/syncher/EntityDataAccessor
        getstatic net/minecraft/world/item/ItemStack.EMPTY net/minecraft/world/item/ItemStack
        invokevirtual net/minecraft/network/syncher/SynchedEntityData.define(net/minecraft/network/syncher/EntityDataAccessor,java/lang/Object)V

        return
    }

    public getPickResult()net/minecraft/world/item/ItemStack {
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getItem()net/minecraft/world/item/ItemStack
        areturn
    }

    public canBeLeashed(net/minecraft/world/entity/player/Player)Z {
        iconst 0
        ireturn
    }

    public getItem()net/minecraft/world/item/ItemStack {
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getEntityData()net/minecraft/network/syncher/SynchedEntityData
        getstatic io/github/lukebemish/lesslazyitems/WalkingItemEntity.DATA_ITEM net/minecraft/network/syncher/EntityDataAccessor
        invokevirtual net/minecraft/network/syncher/SynchedEntityData.get(net/minecraft/network/syncher/EntityDataAccessor)java/lang/Object
        checkcast net/minecraft/world/item/ItemStack
        areturn
    }

    public setItem(net/minecraft/world/item/ItemStack)V {
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getEntityData()net/minecraft/network/syncher/SynchedEntityData
        getstatic io/github/lukebemish/lesslazyitems/WalkingItemEntity.DATA_ITEM net/minecraft/network/syncher/EntityDataAccessor
        aload 1
        invokevirtual net/minecraft/network/syncher/SynchedEntityData.set(net/minecraft/network/syncher/EntityDataAccessor,java/lang/Object)V
        return
    }

    public getAge()I {
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.age I
        ireturn
    }

    public fireImmune()Z {
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getItem()net/minecraft/world/item/ItemStack
        invokevirtual net/minecraft/world/item/ItemStack.getItem()net/minecraft/world/item/Item
        invokevirtual net/minecraft/world/item/Item.isFireResistant()Z
        ifne break
        aload 0
        invokespecial net/minecraft/world/entity/Entity.fireImmune()Z
        ireturn

        break:
        iconst 1
        ireturn
    }

    public addAdditionalSaveData(net/minecraft/nbt/CompoundTag)V {
        aload 0
        aload 1
        invokespecial net/minecraft/world/entity/PathfinderMob.addAdditionalSaveData(net/minecraft/nbt/CompoundTag)V

        aload 1
        ldc "Age"
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.age I
        i2s
        invokevirtual net/minecraft/nbt/CompoundTag.putShort(java/lang/String,short)V

        aload 1
        ldc "ItemAge"
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.itemAge I
        i2s
        invokevirtual net/minecraft/nbt/CompoundTag.putShort(java/lang/String,short)V

        aload 1
        ldc "Item"
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getItem()net/minecraft/world/item/ItemStack
        new net/minecraft/nbt/CompoundTag
        dup
        invokespecial net/minecraft/nbt/CompoundTag.<init>()V
        invokevirtual net/minecraft/world/item/ItemStack.save(net/minecraft/nbt/CompoundTag)net/minecraft/nbt/CompoundTag
        invokevirtual net/minecraft/nbt/CompoundTag.put(java/lang/String,net/minecraft/nbt/Tag)net/minecraft/nbt/Tag
        pop
        return
    }

    public readAdditionalSaveData(net/minecraft/nbt/CompoundTag)V {
        aload 0
        aload 1
        invokespecial net/minecraft/world/entity/PathfinderMob.readAdditionalSaveData(net/minecraft/nbt/CompoundTag)V

        aload 0
        aload 1
        ldc "Age"
        invokevirtual net/minecraft/nbt/CompoundTag.getShort(java/lang/String)S
        putfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.age I

        aload 0
        aload 1
        ldc "ItemAge"
        invokevirtual net/minecraft/nbt/CompoundTag.getShort(java/lang/String)S
        putfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.itemAge I

        aload 1
        ldc "Item"
        invokevirtual net/minecraft/nbt/CompoundTag.getCompound(java/lang/String)net/minecraft/nbt/CompoundTag
        invokestatic net/minecraft/world/item/ItemStack.of(net/minecraft/nbt/CompoundTag)net/minecraft/world/item/ItemStack
        dup
        aload 0
        swap
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.setItem(net/minecraft/world/item/ItemStack)V

        invokevirtual net/minecraft/world/item/ItemStack.isEmpty()Z
        ifeq out
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.discard()V

        out:
        return
    }

    public getName()net/minecraft/network/chat/Component {
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getCustomName()net/minecraft/network/chat/Component
        dup
        ifnull skip
        areturn

        skip:
        pop
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getItem()net/minecraft/world/item/ItemStack
        invokevirtual net/minecraft/world/item/ItemStack.getDescriptionId()java/lang/String
        invokestatic* net/minecraft/network/chat/Component.translatable(java/lang/String)net/minecraft/network/chat/MutableComponent
        areturn
    }

    public isAttackable()Z {
        iconst 0
        ireturn
    }

    public onSyncedDataUpdated(net/minecraft/network/syncher/EntityDataAccessor)V {
        aload 0
        aload 1
        invokespecial net/minecraft/world/entity/Entity.onSyncedDataUpdated(net/minecraft/network/syncher/EntityDataAccessor)V
        getstatic io/github/lukebemish/lesslazyitems/WalkingItemEntity.DATA_ITEM net/minecraft/network/syncher/EntityDataAccessor
        aload 1
        invokevirtual net/minecraft/network/syncher/EntityDataAccessor.equals(java/lang/Object)Z
        ifeq out
        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getItem()net/minecraft/world/item/ItemStack
        aload 0
        invokevirtual net/minecraft/world/item/ItemStack.setEntityRepresentation(net/minecraft/world/entity/Entity)V

        out:
        return
    }

    public getSoundSource()net/minecraft/sounds/SoundSource {
        getstatic net/minecraft/sounds/SoundSource.AMBIENT net/minecraft/sounds/SoundSource
        areturn
    }

    public getVisualRotationYInDegrees()F {
        ldc 180.0
        aload 0
        ldc 0.5
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getSpin(F)F
        getstatic java/lang/Math.PI D
        d2f
        ldc 2.0
        fmul
        fdiv
        ldc 360.0
        fmul
        fsub
        freturn
    }

    public getSpin(F)F {
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.age I
        i2f
        fload 1
        fadd
        ldc 20.0
        fdiv
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.bobOffs F
        fadd
        freturn
    }

    public static createAttributes()net/minecraft/world/entity/ai/attributes/AttributeSupplier$Builder {
        invokestatic net/minecraft/world/entity/Mob.createMobAttributes()net/minecraft/world/entity/ai/attributes/AttributeSupplier$Builder

        getstatic net/minecraft/world/entity/ai/attributes/Attributes.MAX_HEALTH net/minecraft/world/entity/ai/attributes/Attribute
        bipush 10
        i2d
        invokevirtual net/minecraft/world/entity/ai/attributes/AttributeSupplier$Builder.add(net/minecraft/world/entity/ai/attributes/Attribute,D)net/minecraft/world/entity/ai/attributes/AttributeSupplier$Builder

        getstatic net/minecraft/world/entity/ai/attributes/Attributes.MOVEMENT_SPEED net/minecraft/world/entity/ai/attributes/Attribute
        ldc 0.3
        f2d
        invokevirtual net/minecraft/world/entity/ai/attributes/AttributeSupplier$Builder.add(net/minecraft/world/entity/ai/attributes/Attribute,D)net/minecraft/world/entity/ai/attributes/AttributeSupplier$Builder

        areturn
    }

    protected dropAllDeathLoot(net/minecraft/world/damagesource/DamageSource)V {
        aload 0
        aload 1
        invokespecial net/minecraft/world/entity/PathfinderMob.dropAllDeathLoot(net/minecraft/world/damagesource/DamageSource)V

        aload 0
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.createItemEntity()V
        return
    }

    public createItemEntity()V {
        aload 0
        dup
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.getItem()net/minecraft/world/item/ItemStack
        invokevirtual io/github/lukebemish/lesslazyitems/WalkingItemEntity.spawnAtLocation(net/minecraft/world/item/ItemStack)net/minecraft/world/entity/item/ItemEntity
        dup
        ifnull passed
        dup
        getfield net/minecraft/world/entity/item/ItemEntity.lifespan I
        aload 0
        getfield io/github/lukebemish/lesslazyitems/WalkingItemEntity.itemAge I
        isub
        dup
        ifle skip1
        putfield net/minecraft/world/entity/item/ItemEntity.lifespan I
        goto out

        skip1:
        pop
        iconst 1
        putfield net/minecraft/world/entity/item/ItemEntity.lifespan I

        out:
        return

        passed:
        pop
        return
    }
}